pipeline {

  agent {
    docker {
      label 'SpaceJam'
      image '942037308728.dkr.ecr.us-west-1.amazonaws.com/spacejam-builds:latest'
      args '-e npm_config_cache=/npm_cache -v /opt/jenkins/cache:/npm_cache'
    }
  }

  options {
    gitLabConnection('E84 Gitlab')
  }

  environment {
    AWS_DEFAULT_REGION = 'us-west-2'
    AWS_REGION = 'us-west-2'
  }

  stages {

    stage('Prepare'){

      steps {
        echo sh(returnStdout: true, script: 'env')

        updateCommitStatus('pending')

        sshagent(credentials : ['spacejam-buildserver']) {

          sh 'mkdir ~/.ssh'
          sh 'touch ~/.ssh/config'
          sh 'echo "StrictHostKeyChecking no" >> ~/.ssh/config'

          updateCommitStatus('running')

          echo "Installing..."
          sh 'yarn install'

          echo "Testing..."
          sh 'yarn test'

        }

      }

    }

    stage('Versioning'){
      // bumping version number for the develop pipeline
      when {
        branch 'develop'
      }
      steps {
        sshagent(credentials : ['spacejam-buildserver']) {
          // Without 'force', bump will fail if it doesn't think
          // there is anything to put in the changelog.

          // Note this assumes the remote is gitlab -
          // in bamboo for instance this doesn't work because
          // the remote is a local cached copy of the git index

          // In order for the branch to be known, Jenkins
          // needs this setting on the git plugin:
          //   "Check out to matching local branch"

          sh """git config user.email "buildserver@element84.com"
               |git config user.name "Jenkins"
               |GIT_COMMIT_MESSAGE=`git log -n 1 --pretty=%s $GIT_COMMIT`
               |if [[ "\$GIT_COMMIT_MESSAGE" == "Merge branch "* ]]; then
               |  yarn bump "patch" --force
               |  git config push.default current
               |  git push
               |else
               |  echo "Not a merge request - not bumping version"
               |fi
           """.stripMargin()
        }
      }
    }

    stage('Build'){

      steps {

        // The actual build...

        sh 'yarn build'
        sh 'mkdir -p artifacts'
        sh 'tar --exclude="./artifacts" -czf artifacts/dashboard.tgz public'
        archiveArtifacts artifacts: 'artifacts/dashboard.tgz', fingerprint: true

      }
    }

    stage('Deploy Feature') {
      when { branch 'feature/*' }
      steps {
        deployFeature()
      }
    }

    stage('Deploy SIT') {
      when { branch 'develop' }
      steps {
        deploySIT()
      }
    }

  }

  post {
    always {
      echo 'Cleaning workspace...'
       deleteDir()
     }
     success {
      updateCommitStatus('success')
     }
     failure {
      updateCommitStatus('failed')
      notifySlack('failure')
     }
  }

}

void deployFeature() {
  sh 'echo "Feature"'
  script {
    echo("Branch: " + env.BRANCH_NAME)
    def match = (env.BRANCH_NAME =~ /^(?i)feature\/((jam|fd)-\d+).*/)

    if(!match.hasGroup() || match.size() == 0) {
          error "Unable to find Jira ticket in branch name"
    }
    env.JIRA_TICKET = match[0][1]
  }
  performDeploy("${JIRA_TICKET}", 'dev', true)
}

void deploySIT() {
  sh 'echo "SIT"'
  performDeploy('sit', 'sit')
}

void deployUAT() {
  sh 'echo "UAT"'
  performDeploy('uat', 'uat')
}

void performDeploy(environment, credentials_env = 'dev', createBucket = false) {
  env.target_environment = environment.toLowerCase()
  env.target_bucket = "capella-dashboard-components-${target_environment}"
  env.s3_bucket = "http://${target_bucket}.s3-website-us-west-2.amazonaws.com"

  withAWS(credentials: "deploy-${credentials_env}-dashboard", region:'us-west-2') {

    if(createBucket) {
      sh '''
          cd build-deploy && \
          sh ./create-bucket.sh "${target_environment}" "${target_bucket}"
          aws lambda invoke --function-name pipeline-service-live-initializeNewEnvironment --region us-west-2 --log-type Tail --payload '{"key":"'${JIRA_TICKET}'", "url":"'${s3_bucket}'"}' /tmp/outfile-${JIRA_TICKET}.txt && cat "/tmp/outfile-${JIRA_TICKET}.txt" && echo "${JIRA_TICKET}\n"
         '''
    }
    echo 'Deleting old files from S3...'

    s3Delete bucket: "${target_bucket}", path: '/'

    echo 'Uploading new files to S3...'

    s3Upload(file: 'storybook-static', bucket: "${target_bucket}")

  }

  notifySlack('success')

}

void updateCommitStatus(status) {
  updateGitlabCommitStatus name: 'build', state: "$status"
}

void notifySlack(status = 'success') {

  color = 'good'
  channel = '#space-jam-deploys'
  job = "${env.JOB_NAME} - ${env.BRANCH_NAME} - ${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"
  message = "Deployed to ${env.target_environment} - ${env.s3_bucket} - ${job} :pikachu:"

  if ( status == 'failure' ) {
    color = '#FF0000'
    message = "Build Failed - ${job} :alert:"
  }

  slackSend color: color, channel: channel, message: message

}